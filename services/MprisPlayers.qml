pragma Singleton

import QtQuick
import Quickshell
import Quickshell.Services.Mpris

Singleton {
    id: root

    readonly property list<MprisPlayer> playerList: Mpris.players.values
    property MprisPlayer selectedPlayer: playerList.length > 0 ? playerList[activeIndex] : null
    readonly property MprisPlayer activePlayer: selectedPlayer
    property int activeIndex: 0
    property string playerName: selectedPlayer?.identity ?? ""

    onPlayerListChanged: {
        if (!playerList || playerList.length === 0) {
            activeIndex = 0;
            return;
        }
        if (playerName !== "") {
            const index = playerList.findIndex(item => item && item.identity === playerName);
            activeIndex = index !== -1 ? index : 0;
        } else {
            activeIndex = 0;
        }
    }

    onActivePlayerChanged: {
        if (activePlayer) {
            activePlayer.positionChanged();
        }
    }
    readonly property real percentageProgress: {
        if (!activePlayer || !activePlayer.lengthSupported || activePlayer.length <= 0)
            return 0;
        return activePlayer.position / activePlayer.length;
    }

    function selectPlayer(x: int) {
        if (!playerList || playerList.length === 0) {
            activeIndex = 0;
            return;
        }
        activeIndex = (activeIndex + x + playerList.length) % playerList.length;
    }

    FrameAnimation {
        id: idMprisFrameAnimation

        running: root.activePlayer && root.activePlayer.playbackState === MprisPlaybackState.Playing
        onTriggered: if (root.activePlayer) {
            root.activePlayer.positionChanged();
        }
    }
}
